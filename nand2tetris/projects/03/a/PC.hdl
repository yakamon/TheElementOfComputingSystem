// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Add16(a[0]=true, a[1..15]=false, b=Out, out=Inc);
    Mux16(a=Out, b=Inc, sel=inc, out=IncOut);
    Mux16(a=IncOut, b=in, sel=load, out=InOut);
    Mux16(a=InOut, b=false, sel=reset, out=ResetOut);
    Register(in=ResetOut, load=true, out=Out, out=out);
}
